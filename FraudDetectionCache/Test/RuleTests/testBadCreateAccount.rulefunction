/**
 * @description 
 */
void rulefunction Test.RuleTests.testBadCreateAccount {
	attribute {
		validity = ACTION;
	}
	scope {
		
	}
	body {
        Object logger = Log.getLogger("Test.RuleTests.testBadCreateAccount");
        Log.log(logger, "info", "Start Test.RuleTests.testBadCreateAccount");
        
        String acctId = "BadCreateAccount";
        cleanupAccount(acctId);
        
        // create account for the test
        Concepts.Account.Account(
            acctId /*extId String */,
            -100 /*Balance double */,
            0 /*Debits double */,
            "Suspended" /*Status String */,
            2000 /*AvgMonthlyBalance double */);
        Engine.executeRules();
        
        // load account into working memory
        Account acct = Cluster.DataGrid.CacheLoadConceptByExtId(acctId, true);
        Events.CreateAccount evt = Events.CreateAccount.CreateAccount(
            null /*extId String */,
            null /*payload String */,
            acctId /*AccountId String */,
            2000 /*AvgMonthlyBalance double */,
            20000 /*Balance double */);
            
        // execute all rules for debit event, but only the BadCreateAccount rule will be triggered
        Event.assertEvent(evt);
        Engine.executeRules();
        
        // reload updated account to verify the update
        acct = Cluster.DataGrid.CacheLoadConceptByExtId(acctId, true);
        assertNotNull(String.format("Account %s exists", acctId), acct);
        assertWithinRange("Account balance is not updated", -100, acct.Balance, 0.001);
        assertThat("Account status is not updated", acct.Status, equalTo("Suspended"));
       
        Log.log(logger, "info", "Completed Test.RuleTests.testBadCreateAccount");       		
	}
}